fn is_valid(key, val) {
  # // TODO: Make dictionaries
  # rules = {
  #   "byr": fn(val) 1920 <= val and val <= 2002,
  #   "iyr": fn(val) 2010 <= val and val <= 2020,
  #   "eyr": fn(val) 2020 <= val and val <= 2030,
  #   ...
  # };

  if key == "byr" { return 1920 <= int(val) and int(val) <= 2002 }
  if key == "iyr" { return 2010 <= int(val) and int(val) <= 2020 }
  if key == "eyr" { return 2020 <= int(val) and int(val) <= 2030 }
  if key == "hgt" {
    num, unit = val.find(/^(\d+)(cm|in)$/n) or (return false);
    return (unit == "cm" and 150 <= num and num <= 193)
        or (unit == "in" and 59 <= num and num <= 76);
  }
  if key == "hcl" { return val.is_match(/^#[0-9a-f]{6}$/) }
  if key == "ecl" { return val.is_match(/amb|blu|brn|gry|grn|hzl|oth/) }
  if key == "pid" { return val.is_match(/^\d{9}$/) }
};

part1, part2 = (0, 0);
for passport in input().split("\n\n") {
  fields = passport.find_all(/(\w+):(\S+)/);

  all_fields, all_valid = (true, true);
  for req in ["byr", "iyr", "eyr", "hgt", "hcl", "ecl", "pid"] {
    present, valid = (false, false);
    for field in fields {
      key, val = field;
      if key == req {
        valid = is_valid(key, val);
        present = true;
        break;
      };
    };

    all_fields = false if not present;
    all_valid = false if not valid;
  };

  part1 += 1 if all_fields;
  part2 += 1 if all_valid;
};

print("Part 1: " + part1);
print("Part 2: " + part2);
